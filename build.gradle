buildscript {
    ext.kotlin_version = '1.2.41'
    ext.ktor_version = '0.9.2'
    ext.slf4j_version = '1.7.25'
    ext.logback_version = '1.2.1'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "us.kirchmeier.capsule" version "1.0.2"
    id 'com.google.cloud.tools.jib' version '0.9.6'
}

group 'com.kanelai'
version '1.0.0'

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'com.kanelai.eris.eventapi.App'
(getTasksByName("run", false).first() as JavaExec).jvmArgs("-Dconfig.file=config/eris-event-api.conf", "-Dlogback.configurationFile=config/logback.xml")

sourceCompatibility = 1.8
compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

kotlin.experimental.coroutines = 'enable'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/exposed" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // server
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"
    compile "io.ktor:ktor-metrics:$ktor_version"

    // client
    compile "io.ktor:ktor-client-core:$ktor_version"
    compile "io.ktor:ktor-client-apache:$ktor_version"
    compile "io.ktor:ktor-client-json:$ktor_version"

    compile "org.slf4j:slf4j-api:$slf4j_version"
    runtime "ch.qos.logback:logback-classic:$logback_version"

    compile "com.typesafe:config:1.3.2"

    compile "org.jetbrains.exposed:exposed:0.10.2"
    compile "com.zaxxer:HikariCP:2.7.8"
    runtime "com.h2database:h2:1.4.197"
    runtime "mysql:mysql-connector-java:8.0.11"

    compile "com.google.code.gson:gson:2.8.3"

    testCompile "junit:junit:4.12"
}

task fatCapsule(type: FatCapsule) {
    applicationClass mainClassName
    capsuleManifest {
        //systemProperties['log4j.configuration'] = 'log4j.xml'
        jvmArgs = ["-Dconfig.file=config/eris-event-api.conf", "-Dlogback.configurationFile=config/logback.xml"]
        //args = []
        minJavaVersion = '1.8.0'
    }
}

task generateJks(type: JavaExec, dependsOn: 'classes') {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.kanelai.eris.eventapi.dev.CertificateGenerator'
}

getTasksByName("run", false).first().dependsOn('generateJks')

// Jib
//tasks.build.dependsOn tasks.jibDockerBuild
jib {
    container {
        jvmFlags = ['-Xms512m', "-Dconfig.file=config/eris-event-api.conf", "-Dlogback.configurationFile=config/logback.xml"]
        mainClass = mainClassName
        //args = []
        ports = ['8080', '8443']
        format = 'OCI'
    }
}
